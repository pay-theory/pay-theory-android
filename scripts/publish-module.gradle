apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Pay Theory Android SDK'
                    url = 'https://github.com/pay-theory/pay-theory-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/pay-theory/pay-theory-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'abelcollins'
                            name = 'Abel Collins'
                            email = 'abel@paytheory.com'
                        }
                    }
                    // Version control info
                    scm {
                        connection = 'scm:git:github.com/pay-theory/pay-theory-android.git'
                        developerConnection = 'scm:git:ssh://github.com/pay-theory/pay-theory-android.git'
                        url = 'https://github.com/pay-theory/pay-theory-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    rootProject.ext["signing.keyId"]
    rootProject.ext["signing.secretKeyRingFile"]
    rootProject.ext["signing.password"]
    sign publishing.publications
}