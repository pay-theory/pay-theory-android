import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '2.0.21'
    repositories {
        maven {
            url "${artifactory_context_url}pay-theory-android-gradle-release-local"
            credentials {
                username = "$artifactory_user"
                password = "$artifactory_password"
            }
        }


        google()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:5.2.5"
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"

        //Dokka (gradlew :PayTheorySDK:dokka)
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.9.20"

        //Jacoco
        classpath "org.jacoco:org.jacoco.core:0.8.7"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
    // Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.


}

plugins {
    id 'com.jfrog.artifactory' version "5.2.5" apply false
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "org.jetbrains.dokka" version "1.9.20"
    id 'org.jetbrains.kotlin.android' version '2.0.21' apply false
}
allprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        }
    }
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    version = currentVersion
    repositories {
        maven {
            url "${artifactory_context_url}pay-theory-android-gradle-release-local"
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }

        google()
        mavenCentral()
        mavenLocal()
    }
}
artifactoryPublish.skip = true
project('testsdk') {
    artifactoryPublish.skip = true
}

project('AndroidSDK') {
    artifactoryPublish.dependsOn('build')
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = group
                artifactId = project.getName()
                version = currentVersion
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}


artifactory {
    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.addEnvironmentProperty('test.adding.dynVar', new Date().toString())

    publish {
        contextUrl = "$artifactory_context_url"   //The base Artifactory URL if not overridden by the publisher/resolver
        publish {
            repository {
                repoKey = 'pay-theory-android-gradle-release-local'
                username = "$artifactory_user"
                password = "$artifactory_password"
                ivy {
                    ivyLayout = '[organization]/[module]/[revision]/[module]-[revision](-[classifier]).pom'
                    artifactLayout = '[organization]/[module]/[revision]/[module]-[revision](-[classifier]).[ext]'
                    mavenCompatible = true
                }
            }
        }
        defaults {
            publishArtifacts = true
            publishPom = true
        }
    }
}