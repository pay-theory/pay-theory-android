

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

File secretPropsFile = project.rootProject.file('local.properties')

Properties p = new Properties()
new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
p.each { name, value -> ext[name] = value }

android {

    namespace = "com.paytheory.android.sdk"

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    compileSdk = 35

    buildFeatures {
        prefab = true
    }
    defaultConfig {
        minSdkVersion 29
        targetSdk = 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        aarMetadata {
            minCompileSdk = 29
        }

//        buildConfigField "String", "GOOGLE_PROJECT_NUMBER", "\"${System.getenv("GOOGLE_PROJECT_NUMBER")}\""
    }
    buildTypes {
        debug {
            resValue("string", "google_project_number", "${GOOGLE_PROJECT_NUMBER}")
        }
        release {
            resValue("string", "google_project_number", "${GOOGLE_PROJECT_NUMBER}")
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Kotlin Usage
    implementation 'androidx.core:core-ktx:1.15.0'
    // Appcompat Layouts
    implementation 'androidx.appcompat:appcompat:1.7.0'
    // lifecycle (ViewModel, LiveData, Extension)
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //Google UI
    implementation 'com.google.android.material:material:1.12.0'
    //Gson
    implementation 'com.google.code.gson:gson:2.11.0'
    //Okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.14'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.14'
    //Lazy sodium
    implementation 'com.goterl:lazysodium-android:5.1.0@aar'
    implementation 'net.java.dev.jna:jna:5.15.0@aar'
    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.11.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.11.0'
    //Rx Java
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.9'
    //Google Play services
    implementation 'com.google.android.gms:play-services-base:18.5.0'
    //AWS Android SDK
    implementation 'com.amazonaws:aws-android-sdk-kms:2.77.0'
    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'
    implementation "org.jetbrains.kotlin:kotlin-script-runtime:2.0.21"
    implementation "androidx.multidex:multidex:2.0.1"
    //Google Play Integrity Api
    implementation 'com.google.android.play:integrity:1.4.0'

    implementation 'com.caverock:androidsvg-aar:1.4'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                groupId = 'com.paytheory.android.sdk'
                artifactId = 'AndroidSDK'
                version = '3.0.4'

                pom {
                    packaging = "aar"
                    name = 'Pay Theory SDK'
                    description = 'Pay Theory SDK for Android'
                    url = 'https://github.com/pay-theory/pay-theory-android'
                    inceptionYear = "2021"

                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://opensource.org/licenses/MIT"
                        }
                    }

                    developers {
                        developer {
                            id = 'aron23'
                            name = 'Aron Price'
                            email = 'aron@paytheory.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/pay-theory/pay-theory-android.git'
                        developerConnection = 'scm:git:ssh://github.com/pay-theory/pay-theory-android.git'
                        url = 'https://github.com/pay-theory/pay-theory-android/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}